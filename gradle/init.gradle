import java.util.regex.Matcher

gradle.rootProject {
    task wrapper(type: Wrapper) {
        //set Gradle Version to Wrapper Task
        gradleVersion = "${ver}"

        doLast {
            // The Gradle distribution site only supports https download now.
            def wrapperProps = new Properties()
            propertiesFile.withInputStream { wrapperProps.load(it) }
            def urlStr = wrapperProps.get("distributionUrl")
            if (urlStr =~ /^(http[s]?:\/\/services.gradle.org\/distributions)\/gradle-(.*)-(bin|all).zip/) {
                def prefix = Matcher.lastMatcher.group(1)
                def version = Matcher.lastMatcher.group(2)
                println "${prefix}, ${version}, ${distType}"
                if (null != gwoUrl && !gwoUrl.trim().isEmpty()) {
                    println "[gng] INFO Replacing offical distribution url with ${gwoUrl}"
                    prefix = gwoUrl
                } else if (prefix =~ /http:\/\/(.*)/) {
                    // Earlier version of Gradle wrapper generates distributionUrl of 'http', not 'https'.
                    // So replace http with https since services.gradle.org not allow 'http' anymore.
                    prefix = "https://" + Matcher.lastMatcher.group(1)
                }
                wrapperProps.setProperty("distributionUrl", "${prefix}/gradle-${ver}-${distType}.zip")
                propertiesFile.withOutputStream { wrapperProps.store(it, "Created by GNG") }
            } else {
                println "[gng] WARNING unrecognized distributilUrl: ${urlStr}"
            }
        }
    }
}